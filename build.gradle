plugins {
    id 'application'
}

group = 'org.contikios.cooja'

def javaVersion = 20

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(javaVersion)
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation files('lib/jipv6.jar')
  // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
  implementation 'ch.qos.logback:logback-classic:1.4.11'
  // https://mvnrepository.com/artifact/com.github.cliftonlabs/json-simple
  implementation 'com.github.cliftonlabs:json-simple:4.0.1'
  // https://mvnrepository.com/artifact/de.sciss/syntaxpane
  implementation 'de.sciss:syntaxpane:1.3.0'
  // https://mvnrepository.com/artifact/info.picocli/picocli
  implementation 'info.picocli:picocli:4.7.5'
  // https://mvnrepository.com/artifact/info.picocli/picocli-codegen
  annotationProcessor 'info.picocli:picocli-codegen:4.7.5'
  // https://mvnrepository.com/artifact/org.jdom/jdom2
  implementation 'org.jdom:jdom2:2.0.6.1'
  // https://mvnrepository.com/artifact/org.jfree/jfreechart
  implementation 'org.jfree:jfreechart:1.5.4'
  // https://mvnrepository.com/artifact/org.openjdk.nashorn/nashorn-core
  implementation 'org.openjdk.nashorn:nashorn-core:15.4'
  // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
  implementation 'org.slf4j:slf4j-api:2.0.9'
  // https://mvnrepository.com/artifact/org.swinglabs.swingx/swingx-autocomplete
  implementation 'org.swinglabs.swingx:swingx-autocomplete:1.6.5-1'
}


// FIXME: add test resources.
sourceSets {
  data {
    resources {
      srcDirs = ['tools/coffee-manager']
      include '*.properties'
    }
  }
  main {
    java {
      srcDirs = ['java', 'tools/coffee-manager']
    }
    resources {
      srcDirs = [data.resources, 'config']
    }
  }
}

application {
  mainClass = 'org.contikios.cooja.Main'
  applicationDefaultJvmArgs = ['-Xms400M', '-Xmx2048M',
    // Several Contiki-NG tests crash the JVM without these flags with Java 17,
    // 08-ipv6-unicast.csc is one example. Unclear why, the JVM should not
    // do anything with the pointers in C-land part of ContikiMoteType.
    '-XX:-UseCompressedOops', '-XX:-UseCompressedClassPointers',
    // Enable the preview foreign function interface.
    '--enable-preview', '--enable-native-access', 'ALL-UNNAMED']
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs += ['--enable-preview',
                           "-Aproject=${project.group}/${project.name}"]
}

tasks.withType(AbstractArchiveTask).configureEach {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
}

tasks.register('copyDependencies', Copy) {
  def dir = layout.buildDirectory.dir("libs/lib")
  description = "Copy jar dependencies into ${dir.get()}"
  group = "Build"
  from configurations.runtimeClasspath.files
  into dir
}

// Run Cooja with: java <JVM parameters> -jar build/libs/cooja.jar <Cooja parameters>
jar {
  dependsOn copyDependencies
  manifest {
    attributes 'Main-Class': 'org.contikios.cooja.Main',
      'Class-Path': '. ' + configurations.runtimeClasspath.files.collect { "lib/" + it.getName() }.join(' ')
  }
}

tasks.register('fullJar', Jar) {
  description = "Assembles a jar that contains all dependencies."
  group = "Build"
  archiveClassifier.set('full')
  manifest {
    attributes 'Main-Class': 'org.contikios.cooja.Main',
      'Class-Path': '. ' + configurations.runtimeClasspath.files.collect { "lib/" + it.getName() }.join(' ')
  }

  from sourceSets.main.output

  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
  }
}

run {
  // Bad Cooja location detected with gradle run, explicitly pass -cooja.
  doFirst {
    args += ['--cooja', "$projectDir"]
  }
  // Connect stdin to make the MSPSim CLI work, except when running in CI.
  if (System.getenv('CI') == null) {
    standardInput = System.in
  }
  // Pass all command line "-Dcooja.k=v" as "-Dk=v" to Cooja.
  System.properties.each { k,v ->
    if (k.startsWith("cooja.")) {
      systemProperty k - "cooja.", v
    }
  }
  systemProperty 'picocli.disable.closures', "true"
  // Enable assertions with "-Passertions".
  enableAssertions = project.hasProperty('assertions')
  // Prevent Gradle from changing directory to tools/cooja.
  workingDir = System.getProperty("user.dir")
}
